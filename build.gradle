plugins {
    id 'java-gradle-plugin'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'maven-publish'
}

group 'com.gluonhq'
version '0.0.2-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "https://nexus.gluonhq.com/nexus/content/repositories/releases"
    }
    maven {
        url "https://nexus.gluonhq.com/nexus/content/repositories/public-snapshots"
    }
}

dependencies {
    implementation gradleApi()

    implementation 'com.gluonhq:omega:0.0.6-SNAPSHOT'
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'com.gluonhq.client-gradle-plugin'
            displayName = 'Gluon Client Plugin'
            description = 'Plugin that simplifies using Gluon Client for JavaFX projects'
            implementationClass = 'com.gluonhq.gradle.ClientPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/gluonhq/client-gradle-plugin'
    vcsUrl = 'https://github.com/gluonhq/client-gradle-plugin'
    tags = [ 'java', 'javafx', 'gluon', 'client', 'graal', 'aot' ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'client-gradle-plugin'
            version = project.version
        }
    }
    repositories {
        maven {
           def repositoryUrl = project.findProperty('repositoryUrl') ?: 'https://nexus.gluonhq.com/nexus/content/repositories/public-snapshots/'
           if (project.hasProperty('gluonNexusUsername') && project.hasProperty('gluonNexusPassword')) {
               url repositoryUrl
               credentials {
                   username project.property('gluonNexusUsername')
                   password project.property('gluonNexusPassword')
               }   
           }
      }
  }
}

license {
    skipExistingHeaders = true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
